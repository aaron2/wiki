#!/usr/local/bin/tclsh8.5

package require sqlite3

proc wiki {{path {}}} {
    set home [file normalize [file dirname $::argv0]]
    set db [file join $path wiki.db]
    if {[file exists $db]} {
        puts "$db exists"
        exit 1
    }
    sqlite3 new $db
    
    set fh [open [file join $home schema] r]
    new eval [read $fh]
    close $fh
    
    set settings {
        TF "%H:%M %m/%d/%Y"
        NAME "wiki: "
        TZ America/Los_Angeles
        FILTER_HTML 1
        HTML_WHITELIST "a b br center dd div dt h1 h2 h3 h4 h5 hr i img li ol p pre span strike table td th tr u ul"
        ANON_CREATE 1
        DEFAULT_LEVEL 00133
        EXPIRES "7 days"
        RECAPTCHA_PRIVATE_KEY ""
        RECAPTCHA_PUBLIC_KEY ""
    }
    
    foreach {key val} $settings {
        new eval {insert into settings (name,val) values($key,$val)}
    }
    
    # pass is password
    new eval {insert into users (user,password,created,level) values('admin','5F4DCC3B5AA765D61D8327DEB882CF99',datetime('now'),'4 {fc fd nc nd ua um up wc}')}
    
    #if {![new exists {select node from tags where name='wiki:home'}]} {
    #    set name Home
    #    set content "Welcome to your new wiki!"
    #    new eval {insert into nodes (name,content,parsed,level,modified,created) values($name,$content,$content,0,datetime('now'),datetime('now'))}
    #    set home [new last_insert_rowid]
    #    new eval {insert into tags (name,node) values('wiki:home',$home)}
    #}
    
    return $db
}

proc fts {{path {}}} {
    set home [file normalize [file dirname $::argv0]]
    if {[file exists [file join $path wiki.db]] && [file exists [file join $home rebuild_ft_index]]} {
        exec [file join $home rebuild_ft_index] $path
        puts "rebuilt fts.db from wiki.db"
    } elseif {![file exists [file join $path fts.db]]} {
        sqlite3 fts [file join $path fts.db]
        fts eval {create virtual table search using fts3('id','name','level','content', tokenize porter)}
    }
}


wiki [lindex $argv 0]
fts [lindex $argv 0]
